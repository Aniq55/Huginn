*** Settings ***
#Suite Setup    Start Huginn
#Suite Teardown    Stop Huginn
Test Setup    Start Huginn
Test Teardown    Stop Huginn
Library    Collections
Library    RequestsLibrary
Resource    Huginn.robot

*** Test Cases ***
GPS endpoint returns the gps data
    [Documentation]    This test checks if the gps endpoint returns the gps data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/gps
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  latitude
    JSON Response Should Contain item    ${resp}  longitude
    JSON Response Should Contain item    ${resp}  altitude
    JSON Response Should Contain item    ${resp}  airspeed
    JSON Response Should Contain item    ${resp}  heading
    Value Close To    ${resp.json()['latitude']}  37.923255  0.001
    Value Close To    ${resp.json()['longitude']}  23.921773  0.001
    Value Close To    ${resp.json()['altitude']}  300.00000  10.0
    Value Close To    ${resp.json()['airspeed']}  30.00000  5.0
    Value Close To    ${resp.json()['heading']}  45.00000  5.0
 
Accelerometer endpoint returns the accelerometer data
    [Documentation]    This test checks if the accelerometer endpoint returns the accelerometer data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/accelerometer
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  x_acceleration
    JSON Response Should Contain item    ${resp}  y_acceleration
    JSON Response Should Contain item    ${resp}  z_acceleration
    Value Close To    ${resp.json()['x_acceleration']}  0.0  3.0
    Value Close To    ${resp.json()['y_acceleration']}  0.0  3.0
    Value Close To    ${resp.json()['z_acceleration']}  -9.8  3.0

Gyroscope endpoint returns the gyroscope data
    [Documentation]    This test checks if the gyroscope endpoint returns the gyroscope data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/gyroscope
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  roll_rate
    JSON Response Should Contain item    ${resp}  pitch_rate
    JSON Response Should Contain item    ${resp}  yaw_rate
    Value Close To    ${resp.json()['roll_rate']}  0.0  5.0
    Value Close To    ${resp.json()['pitch_rate']}  0.0  5.0
    Value Close To    ${resp.json()['yaw_rate']}  0.0  5.0

Thermometer endpoint returns the thermometer data
    [Documentation]    This test checks if the thermometer endpoint returns the thermometer data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/thermometer
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  temperature
    Value Close To    ${resp.json()['temperature']}  280.0  10.0

Pressure sensor endpoint returns the thermometer data
    [Documentation]    This test checks if the pressure sensor endpoint returns the pressure sensor data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/pressure_sensor
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  static_pressure
    Value Close To    ${resp.json()['static_pressure']}  97000.0  5000.0

Pitot tube sensor endpoint returns the pitot tube data
    [Documentation]    This test checks if the pitot tube sensor endpoint returns the pitot tube data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/pitot_tube
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  total_pressure
    Value Close To    ${resp.json()['total_pressure']}  97000.0  5000.0

Inertial navigation system endpoint returns the inertial navigation system data
    [Documentation]    This test checks if the inertial navigation system endpoint returns the inertial navigation system data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/ins
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  latitude
    JSON Response Should Contain item    ${resp}  longitude
    JSON Response Should Contain item    ${resp}  altitude
    JSON Response Should Contain item    ${resp}  airspeed
    JSON Response Should Contain item    ${resp}  heading
    JSON Response Should Contain item    ${resp}  pitch
    JSON Response Should Contain item    ${resp}  roll
    Value Close To    ${resp.json()['latitude']}  37.923255  0.001
    Value Close To    ${resp.json()['longitude']}  23.921773  0.001
    Value Close To    ${resp.json()['altitude']}  300.00000  10.0
    Value Close To    ${resp.json()['airspeed']}  30.00000  5.0
    Value Close To    ${resp.json()['heading']}   45.00000  5.0
    Value Close To    ${resp.json()['roll']}  0.0  3.0
    Value Close To    ${resp.json()['pitch']}  0.0  3.0

Engine endpoint returns the engine data
    [Documentation]    This test checks if the engine endpoint returns the engine data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/engine
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  thrust
    JSON Response Should Contain item    ${resp}  throttle
    Should Be Equal As Numbers    ${resp.json()['throttle']}  0.0

Flight controls endpoint returns the flight controls data
    [Documentation]    This test checks if the flight controls endpoint returns the flight controls data
    [Tags]    api    fdm    sensors
    Create Session    huginn_web_server  ${HUGINN_URL}
    ${resp} =    Get Request    huginn_web_server  /aircraft/flight_controls
    Should be Equal As Strings    ${resp.status_code}  200
    Response Content Type Should Be JSON    ${resp}
    JSON Response Should Contain item    ${resp}  aileron
    JSON Response Should Contain item    ${resp}  elevator
    JSON Response Should Contain item    ${resp}  rudder
    JSON Response Should Contain item    ${resp}  throttle
    Should Be Equal As Numbers    ${resp.json()['aileron']}  0.0
    Should Be Equal As Numbers    ${resp.json()['elevator']}  0.0
    Should Be Equal As Numbers    ${resp.json()['rudder']}  0.0
    Should Be Equal As Numbers    ${resp.json()['throttle']}  0.0
